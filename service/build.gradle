import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'application'
	id 'org.springframework.boot' version "${springBootVersion}"
	id "io.freefair.lombok" version "${ioFreeFairLombok}"
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'fi.poltsi.vempain'
version = project.hasProperty("releaseVersion") ? project.releaseVersion : '0.0.1-SNAPSHOT'

bootJar {
	archiveFileName = "${rootProject.name}-${version}.jar"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of("${javaVersion}")
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url = "https://maven.pkg.github.com/Vempain/vempain-auth"
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
			password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
		}
	}
	maven {
		url = "https://maven.pkg.github.com/Vempain/vempain-admin-backend-api"
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
			password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
		}
	}
	maven { url = 'https://repo.spring.io/milestone' }
}

dependencies {
	implementation project(':api')
	// This needs to be built first in vempain-auth
	implementation "fi.poltsi.vempain:vempain-auth-core:${vempainAuthVersion}"
	implementation "fi.poltsi.vempain:vempain-auth-api:${vempainAuthVersion}"
	implementation "fi.poltsi.vempain:vempain-admin-backend-api:${vempainAdminVersion}"
	// Spring
	implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-webmvc:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-web-services:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-flyway:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-jackson:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
	developmentOnly "org.springframework.boot:spring-boot-docker-compose:${springBootVersion}"
	implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
	// https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1"
	// https://mvnrepository.com/artifact/jakarta.xml.bind/jakarta.xml.bind-api
	implementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.4"
	// https://mvnrepository.com/artifact/jakarta.annotation/jakarta.annotation-api
	implementation "jakarta.annotation:jakarta.annotation-api:3.0.0"
	// https://mvnrepository.com/artifact/commons-codec/commons-codec
	implementation "commons-codec:commons-codec:1.20.0"
	// https://mvnrepository.com/artifact/org.json/json
	implementation "org.json:json:20250517"
	// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-openfeign
	implementation "org.springframework.cloud:spring-cloud-starter-openfeign:5.0.0"
	// https://mvnrepository.com/artifact/net.coobird/thumbnailator
	implementation "net.coobird:thumbnailator:0.4.21"
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	runtimeOnly "org.postgresql:postgresql:42.7.8"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-security-test:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-webmvc-test:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-data-jpa-test:${springBootVersion}"
	testImplementation platform("org.junit:junit-bom:$junitVersion")
	testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
	// https://mvnrepository.com/artifact/org.testcontainers/junit-jupiter
	testImplementation "org.testcontainers:testcontainers-junit-jupiter:${testContainersVersion}"
	testImplementation "org.testcontainers:testcontainers-postgresql:${testContainersVersion}"
	// https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
	testImplementation "org.mockito:mockito-junit-jupiter:5.20.0"
}

dependencyManagement {
	imports {
		// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-dependencies
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.1.0"
	}
}

test {
	useJUnitPlatform()
}

processResources {
	// capture once at configuration time (CC-safe)
	def ver = version.toString()
	// declare as input for configuration cache
	inputs.property("projectVersion", ver)

	filesMatching("**/application.yaml") {
		filteringCharset = "UTF-8"
		filter(ReplaceTokens,
				tokens: [projectVersion: ver]
		)
	}
}
